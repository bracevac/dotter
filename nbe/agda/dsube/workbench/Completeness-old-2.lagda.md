# Completeness of Normalization by Evaluation for D·µâ<:>

We prove completeness with a partial equivalence (PER) model.
Completeness means that whatever terms are Œ≤Œ∑-equal in the D·µâ<:> system,
so will be their normal forms in the PER model of equality (and their NFs exist!)

It seems sufficient to use this result to conclude termination of NbE,
for the special case of relating a well-typed expression to itself.


```agda
module dsube.Completeness where

open import Data.Bool using (true; false)
open import Data.Product using (Œ£; ‚àÉ; Œ£-syntax; ‚àÉ-syntax; _,_; _√ó_)
open import Data.Sum using (_‚äé_; inj‚ÇÅ; inj‚ÇÇ)
open import Function using (_‚àò_; id)
open import Data.Empty using (‚ä• ; ‚ä•-elim)
open import Data.Unit using (‚ä§; tt)
import Relation.Binary.PropositionalEquality as Eq
open Eq using (_‚â°_; refl)
open import Data.Nat using (‚Ñï; zero; suc; _‚â§‚Ä≤_; _<‚Ä≤_)
open import Agda.Primitive using (lzero; lsuc; Level) renaming (_‚äî_ to _‚äîÀ°_)
open import dsube.NbE

-- TODO it seems worthwhile using the stdlib's rich support for binary relations instead
-- of our own ad-hoc definitions
-- Binary relations over a type
Rel‚ÇÇ : ‚àÄ{‚Ñì} ‚Üí Set ‚Ñì ‚Üí Set (lsuc ‚Ñì)
Rel‚ÇÇ {‚Ñì} ùìê  = ùìê ‚Üí ùìê ‚Üí Set ‚Ñì

-- empty relation
‚ä•·¥ø : ‚àÄ {T} ‚Üí Rel‚ÇÇ T
‚ä•·¥ø _ _ = ‚ä•

-- total relation
‚ä§·¥ø : ‚àÄ {T} ‚Üí Rel‚ÇÇ T
‚ä§·¥ø _ _ = ‚ä§

-- Membership of an element in the domain of a given relation
-- TODO makes proofs simpler if we just define it to be R a a, since
-- we only care about PERs, which are by definition reflexive on their domain.
-- Right now, we pointlessly have to duplicate proofs.
_‚àà_ : ‚àÄ{ùìê : Set} ‚Üí ùìê ‚Üí Rel‚ÇÇ ùìê ‚Üí Set
a ‚àà R = ‚àÉ[ a' ]( (R a a') ‚äé (R a' a) )

Dom : ‚àÄ{ùìê : Set} ‚Üí Rel‚ÇÇ ùìê ‚Üí Set
Dom R = ‚àÉ[ a ]( a ‚àà R )

_‚à™_ : ‚àÄ{ùìê : Set} ‚Üí Rel‚ÇÇ ùìê ‚Üí Rel‚ÇÇ ùìê ‚Üí Rel‚ÇÇ ùìê
_‚à™_ R‚ÇÅ R‚ÇÇ a b = R‚ÇÅ a b ‚äé R‚ÇÇ a b

-- fancy notation for semantic equality
_==_‚àà_ : ‚àÄ{ùìê : Set} ‚Üí ùìê ‚Üí ùìê ‚Üí Rel‚ÇÇ ùìê ‚Üí Set
a == b ‚àà R = R a b

memÀ° : ‚àÄ {ùìê}{R : Rel‚ÇÇ ùìê}{a b} ‚Üí R a b ‚Üí a ‚àà R
memÀ° {ùìê} {R} {a} {b} Rab = (b , inj‚ÇÅ Rab)

mem ≥ : ‚àÄ {ùìê}{R : Rel‚ÇÇ ùìê}{a b} ‚Üí R a b ‚Üí b ‚àà R
mem ≥  {ùìê} {R} {a} {b} Rab = (a , inj‚ÇÇ Rab)

-- entailment and equivalence of relations
_‚äÜ_ :  ‚àÄ{ùìê : Set} ‚Üí Rel‚ÇÇ ùìê ‚Üí Rel‚ÇÇ ùìê ‚Üí Set
P ‚äÜ Q = ‚àÄ{a b} ‚Üí P a b ‚Üí Q a b

_‚â°·¥ø_ :  ‚àÄ{ùìê : Set} ‚Üí Rel‚ÇÇ ùìê ‚Üí Rel‚ÇÇ ùìê ‚Üí Set
P ‚â°·¥ø Q = P ‚äÜ Q √ó Q ‚äÜ P

‚â°‚Üí‚â°·¥ø : ‚àÄ{ùìê : Set}{P Q : Rel‚ÇÇ ùìê} ‚Üí P ‚â° Q ‚Üí P ‚â°·¥ø Q
‚â°‚Üí‚â°·¥ø refl = (Œª z ‚Üí z) , Œª z ‚Üí z

‚â°·¥ø-refl : ‚àÄ{ùìê : Set}{R : Rel‚ÇÇ ùìê} ‚Üí R ‚â°·¥ø R
‚â°·¥ø-refl {_} {R} = (Œª x ‚Üí x) , (Œª x ‚Üí x)

‚â°·¥ø-sym : ‚àÄ{ùìê : Set}{R S : Rel‚ÇÇ ùìê} ‚Üí R ‚â°·¥ø S ‚Üí S ‚â°·¥ø R
‚â°·¥ø-sym (R‚äÜS , S‚äÜR) = S‚äÜR , R‚äÜS

‚â°·¥ø-trans : ‚àÄ{ùìê : Set}{R Q S : Rel‚ÇÇ ùìê} ‚Üí R ‚â°·¥ø Q ‚Üí Q ‚â°·¥ø S ‚Üí R ‚â°·¥ø S
‚â°·¥ø-trans (R‚äÜQ , Q‚äÜR) (Q‚äÜS , S‚äÜQ) = (Œª x ‚Üí Q‚äÜS (R‚äÜQ x)) , (Œª x ‚Üí Q‚äÜR (S‚äÜQ x))

‚â°·¥ø-‚àà : ‚àÄ{ùìê : Set}{R S : Rel‚ÇÇ ùìê} ‚Üí R ‚â°·¥ø S ‚Üí ‚àÄ{a b} ‚Üí R a b ‚Üí S a b
‚â°·¥ø-‚àà (fst , _) = fst


-- The types of binary relations on our value domains
Rel = Rel‚ÇÇ ùïç
Rel·¥∫·∂† = Rel‚ÇÇ ùïç·¥∫·∂†
Rel·¥∫·µâ = Rel‚ÇÇ ùïç·¥∫·µâ

-- a partial equivalence relation (PER) is an equivalence relation on its domain
record Per {ùìê} (R : Rel‚ÇÇ ùìê) : Set where
  field -- TODO better add ùìê and R as fields and have Per non-parameterized?
    per-refl  : ‚àÄ {a} ‚Üí a ‚àà R ‚Üí R a a
    per-sym   : ‚àÄ {a a'} ‚Üí R a a' ‚Üí R a' a
    per-trans : ‚àÄ {a b c} ‚Üí R a b ‚Üí R b c ‚Üí R a c
open Per {{...}}

-- these are the top and bottom elements of what Abel dubs a "candidate space".
-- our semantics for equality at the various types lives in the space between these sets
-- that is, ùìëùì∏ùìΩ ‚äÜ ‚ü¶ T ‚üß ‚äÜ ùì£ùì∏ùìπ
-- roughly, we will show that Œì ‚ä¢ e‚ÇÅ ‚âà e‚ÇÇ ‚à∂ T ‚Üí ‚ü¶e‚ÇÅ‚üß == ‚ü¶e‚ÇÇ‚üß ‚àà ‚ü¶ T ‚üß
-- there is a bit more structure needed for dependent types, but that is the high-level intuition
ùì£ùì∏ùìπ : Rel·¥∫·∂†
ùì£ùì∏ùìπ v‚ÇÅ v‚ÇÇ = ‚àÄ n ‚Üí ‚àÉ[ e·¥∫·∂† ]( ‚Ñú·¥∫·∂†‚ü® n ‚ü© v‚ÇÅ ‚áì e·¥∫·∂† √ó ‚Ñú·¥∫·∂†‚ü® n ‚ü© v‚ÇÇ ‚áì e·¥∫·∂† )

ùìëùì∏ùìΩ : Rel·¥∫·µâ
ùìëùì∏ùìΩ nv‚ÇÅ nv‚ÇÇ = ‚àÄ n ‚Üí ‚àÉ[ e·¥∫·µâ ]( ‚Ñú·¥∫·µâ‚ü® n ‚ü© nv‚ÇÅ ‚áì e·¥∫·µâ √ó ‚Ñú·¥∫·µâ‚ü® n ‚ü© nv‚ÇÇ ‚áì e·¥∫·µâ )

--- Bot and Top are PERs
instance Per-ùì£ùì∏ùìπ : Per ùì£ùì∏ùìπ
per-refl  {{Per-ùì£ùì∏ùìπ}} (_ , inj‚ÇÅ Taa') n with Taa' n
... | e·¥∫·∂† , ‚Ñú·¥∫·∂†‚ü®n‚ü©a‚áìe·¥∫·∂† , _ = e·¥∫·∂† , ‚Ñú·¥∫·∂†‚ü®n‚ü©a‚áìe·¥∫·∂† , ‚Ñú·¥∫·∂†‚ü®n‚ü©a‚áìe·¥∫·∂†
per-refl  {{Per-ùì£ùì∏ùìπ}} (_ , inj‚ÇÇ Ta'a) n with Ta'a n
... | e·¥∫·∂† , _ , ‚Ñú·¥∫·∂†‚ü®n‚ü©a‚áìe·¥∫·∂† = e·¥∫·∂† , ‚Ñú·¥∫·∂†‚ü®n‚ü©a‚áìe·¥∫·∂† , ‚Ñú·¥∫·∂†‚ü®n‚ü©a‚áìe·¥∫·∂†
per-sym   {{Per-ùì£ùì∏ùìπ}} Taa' n with Taa' n
... | e·¥∫·∂† , fst , snd = e·¥∫·∂† , snd , fst
per-trans {{Per-ùì£ùì∏ùìπ}} Tab Tbc n  with Tab n | Tbc n
... | eab , fst , snd | ebc , fst‚ÇÅ , snd‚ÇÅ rewrite is-fun-‚Ñú·¥∫·∂† snd fst‚ÇÅ = ebc , fst , snd‚ÇÅ

instance Per-ùìëùì∏ùìΩ : Per ùìëùì∏ùìΩ
per-refl  {{Per-ùìëùì∏ùìΩ}} (_ , inj‚ÇÅ Botaa') n with Botaa' n
... | x , fst , _ = x , fst , fst
per-refl  {{Per-ùìëùì∏ùìΩ}} (_ , inj‚ÇÇ Bota'a) n with Bota'a n
... | x , _ , snd = x , snd , snd
per-sym   {{Per-ùìëùì∏ùìΩ}} Botaa' n with Botaa' n
... | x , fst , snd = x , snd , fst
per-trans {{Per-ùìëùì∏ùìΩ}} Botab Botbc n with Botab n | Botbc n
... | x , fst , snd | y , fst‚ÇÅ , snd‚ÇÅ rewrite is-fun-‚Ñú·¥∫·µâ snd fst‚ÇÅ = y , fst , snd‚ÇÅ

-- TODO show that ùìëùì∏ùìΩ ‚äÜ ùì£ùì∏ùìπ ‚Üí ùìëùì∏ùìΩ, ùìëùì∏ùìΩ ‚Üí ùì£ùì∏ùìπ ‚äÜ ùì£ùì∏ùìπ for the semantic function spaces
-- This is a bit hand-wavy, since  ùìëùì∏ùìΩ and ùì£ùì∏ùìπ are relations over different kinds of domains
-- (neutral values, normal form values), which in turn are different from the values domain.
-- So it's not clear how to define the notions of ‚äÜ and ‚Üí between them.

-- TODO show the various implications analogous to Abel'13, p.32, stemming from the
-- cases of the read-back functions

data ùìùùì™ùìΩ : Rel where
  ùìù-ùëõ : ‚àÄ{n} ‚Üí
        -------------------------------
        (·∂ú (Vùëõ n)) == (·∂ú (Vùëõ n)) ‚àà ùìùùì™ùìΩ

  ùìù-Ne : ‚àÄ{nv nv'} ‚Üí
        nv == nv' ‚àà ùìëùì∏ùìΩ ‚Üí
        -----------------------------------
        ‚Üë‚ü® ·∂ú VùëÅ ‚ü© nv == ‚Üë‚ü® ·∂ú VùëÅ ‚ü© nv' ‚àà ùìùùì™ùìΩ

instance Per-ùìùùì™ùìΩ : Per ùìùùì™ùìΩ
per-refl  {{Per-ùìùùì™ùìΩ}} (_ , inj‚ÇÅ ùìù-ùëõ) = ùìù-ùëõ
per-refl  {{Per-ùìùùì™ùìΩ}} (_ , inj‚ÇÅ (ùìù-Ne {nv} {nv'} x)) = ùìù-Ne (per-refl ( nv' , inj‚ÇÅ x))
per-refl  {{Per-ùìùùì™ùìΩ}} (_ , inj‚ÇÇ ùìù-ùëõ) = ùìù-ùëõ
per-refl  {{Per-ùìùùì™ùìΩ}} (_ , inj‚ÇÇ (ùìù-Ne {nv} {nv'} x)) = ùìù-Ne (per-refl ( nv , inj‚ÇÇ x))
per-sym   {{Per-ùìùùì™ùìΩ}} ùìù-ùëõ = ùìù-ùëõ
per-sym   {{Per-ùìùùì™ùìΩ}} (ùìù-Ne x) = ùìù-Ne (per-sym x)
per-trans {{Per-ùìùùì™ùìΩ}} ùìù-ùëõ ùìù-ùëõ = ùìù-ùëõ
per-trans {{Per-ùìùùì™ùìΩ}} (ùìù-Ne x) (ùìù-Ne x‚ÇÅ) = ùìù-Ne (per-trans x x‚ÇÅ)

-- PERs over a domain indexed by a PER
record _‚à∂_‚üπPer {D : Set} (F : D ‚Üí Rel‚ÇÇ D) (ùìê : Rel‚ÇÇ D) : Set‚ÇÅ where
  field
    respect : ‚àÄ{a a'} ‚Üí ùìê a a' ‚Üí F a ‚â°·¥ø F a'
    per-family : ‚àÄ{a} ‚Üí a ‚àà ùìê ‚Üí Per (F a)
open _‚à∂_‚üπPer

open import dsube.Syntax using (‚Ñí)

-- Neutral type interp -- TODO not sure about the definition: this is one of the very hand-wavy places in thesis
data ùì•Ty-Ne (ùìÅ : ‚Ñí) (NE : ùïç·¥∫·µâ) : Rel where
  ùì•Ty-Ne-mem : ‚àÄ {NE‚ÇÅ ne‚ÇÅ NE‚ÇÇ ne‚ÇÇ} ‚Üí
    NE == NE‚ÇÅ ‚àà ùìëùì∏ùìΩ ‚Üí
    NE == NE‚ÇÇ ‚àà ùìëùì∏ùìΩ ‚Üí
    ne‚ÇÅ == ne‚ÇÇ ‚àà ùìëùì∏ùìΩ ‚Üí
    -------------------------------------------------------------------------------
    ‚Üë‚ü® (‚Üë‚ü® (·∂ú (VùëÜùëíùë° ùìÅ)) ‚ü© NE‚ÇÅ) ‚ü© ne‚ÇÅ == ‚Üë‚ü® (‚Üë‚ü® (·∂ú (VùëÜùëíùë° ùìÅ)) ‚ü© NE‚ÇÇ) ‚ü© ne‚ÇÇ ‚àà (ùì•Ty-Ne ùìÅ NE)

instance Per-ùì•Ty-Ne : ‚àÄ{ùìÅ NE} ‚Üí Per (ùì•Ty-Ne ùìÅ NE)
per-refl  {{Per-ùì•Ty-Ne}} (.(‚Üë‚ü® ‚Üë‚ü® ·∂ú VùëÜùëíùë° _ ‚ü© _ ‚ü© _) , inj‚ÇÅ (ùì•Ty-Ne-mem x x‚ÇÅ x‚ÇÇ)) =
  ùì•Ty-Ne-mem x x (per-refl (memÀ° {_} {ùìëùì∏ùìΩ} x‚ÇÇ))
per-refl  {{Per-ùì•Ty-Ne}} (.(‚Üë‚ü® ‚Üë‚ü® ·∂ú VùëÜùëíùë° _ ‚ü© _ ‚ü© _) , inj‚ÇÇ (ùì•Ty-Ne-mem x x‚ÇÅ x‚ÇÇ)) =
  ùì•Ty-Ne-mem x‚ÇÅ x‚ÇÅ (per-refl (mem ≥ {_} {ùìëùì∏ùìΩ} x‚ÇÇ))
per-sym   {{Per-ùì•Ty-Ne}} (ùì•Ty-Ne-mem x x‚ÇÅ x‚ÇÇ) = ùì•Ty-Ne-mem x‚ÇÅ x (per-sym x‚ÇÇ)
per-trans {{Per-ùì•Ty-Ne}} (ùì•Ty-Ne-mem x x‚ÇÅ x‚ÇÇ) (ùì•Ty-Ne-mem y y‚ÇÅ y‚ÇÇ) = ùì•Ty-Ne-mem x y‚ÇÅ (per-trans x‚ÇÇ y‚ÇÇ)

-- ‚ä• type interp
-- TODO not sure either, but to fulfill the candidate space requirement, V‚ä• cannot be empty
-- it appears reasonable to take the total relation, as interpreting a term at type V‚ä• logically
-- implies arbitrary things
ùì•‚ä• : Rel
ùì•‚ä• _ _ = ‚ä§

instance Per-ùì•‚ä• : Per ùì•‚ä•
per-refl  {{Per-ùì•‚ä•}} _   = tt
per-sym   {{Per-ùì•‚ä•}} _   = tt
per-trans {{Per-ùì•‚ä•}} _ _ = tt

-- ‚ä§ type interp
data ùì•‚ä§ : Rel where
  ùì•‚ä§-ne :  ‚àÄ{nv nv'} ‚Üí
    nv == nv' ‚àà ùìëùì∏ùìΩ ‚Üí
    -----------------------------------
    ‚Üë‚ü® ·∂ú V‚ä§ ‚ü© nv == ‚Üë‚ü® ·∂ú V‚ä§ ‚ü© nv' ‚àà ùì•‚ä§

  ùì•‚ä§-val : ‚àÄ {v} ‚Üí
    ---------------
    v == v ‚àà ùì•‚ä§

instance Per-ùì•‚ä§ : Per ùì•‚ä§
per-refl  {{Per-ùì•‚ä§}} _ = ùì•‚ä§-val
per-sym   {{Per-ùì•‚ä§}} (ùì•‚ä§-ne x) = ùì•‚ä§-ne (per-sym x)
per-sym   {{Per-ùì•‚ä§}} ùì•‚ä§-val = ùì•‚ä§-val
per-trans {{Per-ùì•‚ä§}} (ùì•‚ä§-ne x) (ùì•‚ä§-ne x‚ÇÅ) = ùì•‚ä§-ne (per-trans x x‚ÇÅ)
per-trans {{Per-ùì•‚ä§}} (ùì•‚ä§-ne x) ùì•‚ä§-val = ùì•‚ä§-ne x
per-trans {{Per-ùì•‚ä§}} ùì•‚ä§-val b = b

-- abstract type interp
data ùì•Type (ùì£‚ÇÅ : Rel) (ùì£‚ÇÇ : Rel) : Rel where
  ùì•Type-ne : ‚àÄ {ùëÜ ùëÜ' ùëá ùëá' NE NE'} ‚Üí
          ùëÜ == ùëÜ' ‚àà ùì£‚ÇÅ ‚Üí
          ùëá == ùëá' ‚àà ùì£‚ÇÇ ‚Üí
          NE == NE' ‚àà ùìëùì∏ùìΩ ‚Üí
          ----------------------------------------------------------------
          ‚Üë‚ü® ‚ü™Type ùëÜ ‚ãØ ùëá ‚ü´ ‚ü© NE == ‚Üë‚ü® ‚ü™Type ùëÜ' ‚ãØ ùëá' ‚ü´ ‚ü© NE' ‚àà ùì•Type ùì£‚ÇÅ ùì£‚ÇÇ

  ùì•Type-val : ‚àÄ {ùê¥ ùê¥'} ‚Üí
          ùê¥ == ùê¥' ‚àà ùì£‚ÇÅ ‚Üí
          ùê¥ == ùê¥' ‚àà ùì£‚ÇÇ ‚Üí
          ---------------------------------------
          (VType ùê¥) == (VType ùê¥') ‚àà ùì•Type ùì£‚ÇÅ ùì£‚ÇÇ

instance Per-ùì•Type : ‚àÄ{ùì¢ ùì£ : Rel} ‚Üí {{Per ùì¢}} ‚Üí {{Per ùì£}} ‚Üí Per (ùì•Type ùì¢ ùì£)
per-refl  {{Per-ùì•Type {ùì¢} {ùì£} }} (.(‚Üë‚ü® ‚ü™Type _ ‚ãØ _ ‚ü´ ‚ü© _) , inj‚ÇÅ (ùì•Type-ne x x‚ÇÅ x‚ÇÇ)) =
  ùì•Type-ne (per-refl (memÀ° {_} {ùì¢} x))
            (per-refl (memÀ° {_} {ùì£} x‚ÇÅ))
            (per-refl (memÀ° {_} {ùìëùì∏ùìΩ} x‚ÇÇ))
per-refl  {{Per-ùì•Type {ùì¢} {ùì£} }} (.(VType _) , inj‚ÇÅ (ùì•Type-val {ùê¥} {ùê¥'} x x‚ÇÅ)) =
  ùì•Type-val (per-refl (ùê¥' , inj‚ÇÅ x)) (per-refl (ùê¥' , inj‚ÇÅ x‚ÇÅ))
per-refl  {{Per-ùì•Type {ùì¢} {ùì£} }} (.(‚Üë‚ü® ‚ü™Type _ ‚ãØ _ ‚ü´ ‚ü© _) , inj‚ÇÇ (ùì•Type-ne x x‚ÇÅ x‚ÇÇ)) =
  ùì•Type-ne (per-refl (mem ≥ {_} {ùì¢} x))
            (per-refl (mem ≥ {_} {ùì£} x‚ÇÅ))
            (per-refl (mem ≥ {_} {ùìëùì∏ùìΩ} x‚ÇÇ))
per-refl  {{Per-ùì•Type {ùì¢} {ùì£} }} (.(VType _) , inj‚ÇÇ (ùì•Type-val {ùê¥} {ùê¥'} x x‚ÇÅ)) =
  ùì•Type-val (per-refl (ùê¥ , inj‚ÇÇ x)) (per-refl (ùê¥ , inj‚ÇÇ x‚ÇÅ))
per-sym   {{Per-ùì•Type}} (ùì•Type-ne x x‚ÇÅ x‚ÇÇ) = ùì•Type-ne (per-sym x) (per-sym x‚ÇÅ) (per-sym x‚ÇÇ)
per-sym   {{Per-ùì•Type}} (ùì•Type-val x x‚ÇÅ)   = ùì•Type-val (per-sym x) (per-sym x‚ÇÅ)
per-trans {{Per-ùì•Type}} (ùì•Type-ne x x‚ÇÅ x‚ÇÇ) (ùì•Type-ne x‚ÇÉ x‚ÇÑ x‚ÇÖ) =
  ùì•Type-ne (per-trans x x‚ÇÉ) (per-trans x‚ÇÅ x‚ÇÑ) (per-trans x‚ÇÇ x‚ÇÖ)
per-trans {{Per-ùì•Type}} (ùì•Type-val x x‚ÇÅ) (ùì•Type-val x‚ÇÇ x‚ÇÉ) =
  ùì•Type-val (per-trans x x‚ÇÇ) (per-trans x‚ÇÅ x‚ÇÉ)


-- First, the universe at level 0
data ùì¢ùìÆùìΩ‚ÇÄ : Rel
data ùìîùìµ‚ÇÄ : ùïç ‚Üí Rel

data ùì¢ùìÆùìΩ‚ÇÄ where
  ùì¢ùìÆùìΩ‚ÇÄ-NE : ‚àÄ{NE NE'} ‚Üí
    NE == NE' ‚àà ùìëùì∏ùìΩ ‚Üí
    -------------------------------------------------
    ‚Üë‚ü® (·∂ú (VùëÜùëíùë° 0)) ‚ü© NE == ‚Üë‚ü® (·∂ú (VùëÜùëíùë° 0)) ‚ü© NE' ‚àà ùì¢ùìÆùìΩ‚ÇÄ

  ùì¢ùìÆùìΩ‚ÇÄ-ùëÅ : (·∂ú VùëÅ) == (·∂ú VùëÅ) ‚àà ùì¢ùìÆùìΩ‚ÇÄ

  ùì¢ùìÆùìΩ‚ÇÄ-‚ä§ : (·∂ú V‚ä§) == (·∂ú V‚ä§) ‚àà ùì¢ùìÆùìΩ‚ÇÄ

  ùì¢ùìÆùìΩ‚ÇÄ-‚ä• : (·∂ú V‚ä•) == (·∂ú V‚ä•) ‚àà ùì¢ùìÆùìΩ‚ÇÄ

  ùì¢ùìÆùìΩ‚ÇÄ-Œ† : ‚àÄ{ùê¥ ùê¥' ùêπ ùêπ'} ‚Üí
    ùê¥ == ùê¥' ‚àà ùì¢ùìÆùìΩ‚ÇÄ ‚Üí
    (‚àÄ{ùëé ùëé'} ‚Üí ùëé == ùëé' ‚àà (ùìîùìµ‚ÇÄ ùê¥) ‚Üí
               (‚àÉ[ ùêµ ] ‚àÉ[ ùêµ' ] ( (ùêπ ¬∑ ùëé ‚áì ùêµ) √ó (ùêπ' ¬∑ ùëé' ‚áì ùêµ') √ó (ùêµ == ùêµ' ‚àà ùì¢ùìÆùìΩ‚ÇÄ) ))) ‚Üí
    -----------------------------------------------------------------------------------
    VŒ† ùê¥ ùêπ == VŒ† ùê¥' ùêπ' ‚àà ùì¢ùìÆùìΩ‚ÇÄ

data ùìîùìµ‚ÇÄ where
   ùìîùìµ‚ÇÄ-ùëÅ : ‚àÄ{ùëé ùëè} ‚Üí
           ùëé == ùëè ‚àà ùìùùì™ùìΩ ‚Üí
           -------------------
           ùëé == ùëè ‚àà ùìîùìµ‚ÇÄ (·∂ú VùëÅ)

   ùìîùìµ‚ÇÄ-‚ä§ : ‚àÄ{ùëé ùëè} ‚Üí
           ùëé == ùëè ‚àà ùì•‚ä§ ‚Üí
           -------------------
           ùëé == ùëè ‚àà ùìîùìµ‚ÇÄ (·∂ú V‚ä§)

   ùìîùìµ‚ÇÄ-‚ä• : ‚àÄ{ùëé ùëè} ‚Üí
           ùëé == ùëè ‚àà ùì•‚ä• ‚Üí
           -------------------
           ùëé == ùëè ‚àà ùìîùìµ‚ÇÄ (·∂ú V‚ä•)

   ùìîùìµ‚ÇÄ-Ty-Ne : ‚àÄ {NE ne ne'} ‚Üí
           ne == ne' ‚àà ùì•Ty-Ne 0 NE ‚Üí
           ------------------------------------
           ne == ne' ‚àà ùìîùìµ‚ÇÄ (‚Üë‚ü® (·∂ú (VùëÜùëíùë° 0)) ‚ü© NE)

   ùìîùìµ‚ÇÄ-Œ† : ‚àÄ{ùê¥ ùêπ ùêπ¬∑ùëé ùëì ùëì' ùëé ùëé' ùëè ùëè'} ‚Üí
           ùëé == ùëé' ‚àà ùìîùìµ‚ÇÄ ùê¥ ‚Üí
           ùêπ  ¬∑ ùëé  ‚áì ùêπ¬∑ùëé ‚Üí
           ùëì  ¬∑ ùëé  ‚áì ùëè ‚Üí
           ùëì' ¬∑ ùëé' ‚áì ùëè' ‚Üí
           ùëè == ùëè' ‚àà ùìîùìµ‚ÇÄ ùêπ¬∑ùëé ‚Üí
           ----------------------
           ùëì == ùëì' ‚àà ùìîùìµ‚ÇÄ (VŒ† ùê¥ ùêπ)

data ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ {ùìÄ : ‚Ñí} {ùì¢‚Çñ : Rel} {ùìîùìµ‚Çñ : ùïç ‚Üí Rel} : Rel
data ùìîùìµ‚Çñ‚Çä‚ÇÅ  {ùìÄ : ‚Ñí} {ùì¢‚Çñ : Rel} {ùìîùìµ‚Çñ : ùïç ‚Üí Rel} : ùïç ‚Üí Rel

data ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ {ùìÄ} {ùì¢‚Çñ} {ùìîùìµ‚Çñ} where
  ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-NE : ‚àÄ{NE NE'} ‚Üí
    NE == NE' ‚àà ùìëùì∏ùìΩ ‚Üí
    ------------------------------------------------------------------------
    ‚Üë‚ü® (·∂ú (VùëÜùëíùë° (suc ùìÄ))) ‚ü© NE == ‚Üë‚ü® (·∂ú (VùëÜùëíùë° (suc ùìÄ))) ‚ü© NE' ‚àà ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ

  ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-ùëÅ : (·∂ú VùëÅ) == (·∂ú VùëÅ) ‚àà ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ

  ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-‚ä§ : (·∂ú V‚ä§) == (·∂ú V‚ä§) ‚àà ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ

  ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-‚ä• : (·∂ú V‚ä•) == (·∂ú V‚ä•) ‚àà ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ

  ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-‚äáùì¢‚Çñ : ‚àÄ{ùê¥ ùêµ} ‚Üí
    ùê¥ == ùêµ ‚àà ùì¢‚Çñ ‚Üí
    --------------------
    ùê¥ == ùêµ ‚àà ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ

  ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-‚ü™Type‚ãØ‚ü´ : ‚àÄ{ùëÜ ùëÜ' ùëá ùëá'} ‚Üí
    ùëÜ == ùëÜ' ‚àà ùì¢‚Çñ ‚Üí
    ùëá == ùëá' ‚àà ùì¢‚Çñ ‚Üí
    ------------------------------------------
    ‚ü™Type ùëÜ ‚ãØ ùëá ‚ü´ == ‚ü™Type ùëÜ' ‚ãØ ùëá' ‚ü´ ‚àà ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ

  ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-ùì¢ùìÆùìΩ‚Çñ :
    (·∂ú (VùëÜùëíùë° ùìÄ)) == (·∂ú (VùëÜùëíùë° ùìÄ)) ‚àà ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ

  ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-Œ† : ‚àÄ{ùê¥ ùê¥' ùêπ ùêπ'} ‚Üí
    ùê¥ == ùê¥' ‚àà ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ {ùìÄ} {ùì¢‚Çñ} {ùìîùìµ‚Çñ} ‚Üí
    (‚àÄ{ùëé ùëé'} ‚Üí ùëé == ùëé' ‚àà (ùìîùìµ‚Çñ‚Çä‚ÇÅ {ùìÄ} {ùì¢‚Çñ} {ùìîùìµ‚Çñ} ùê¥) ‚Üí
               (‚àÉ[ ùêµ ] ‚àÉ[ ùêµ' ] ( (ùêπ ¬∑ ùëé ‚áì ùêµ) √ó (ùêπ' ¬∑ ùëé' ‚áì ùêµ') √ó (ùêµ == ùêµ' ‚àà ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ {ùìÄ} {ùì¢‚Çñ} {ùìîùìµ‚Çñ}) ))) ‚Üí
    ----------------------------------------------------------------------------------------------------
    VŒ† ùê¥ ùêπ == VŒ† ùê¥' ùêπ' ‚àà ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ

data ùìîùìµ‚Çñ‚Çä‚ÇÅ {ùìÄ} {ùì¢‚Çñ} {ùìîùìµ‚Çñ} where
   ùìîùìµ‚Çñ‚Çä‚ÇÅ-ùëÅ : ‚àÄ{ùëé ùëè} ‚Üí
           ùëé == ùëè ‚àà ùìùùì™ùìΩ ‚Üí
           --------------------
           ùëé == ùëè ‚àà ùìîùìµ‚Çñ‚Çä‚ÇÅ (·∂ú VùëÅ)

   ùìîùìµ‚Çñ‚Çä‚ÇÅ-‚ä§ : ‚àÄ{ùëé ùëè} ‚Üí
           ùëé == ùëè ‚àà ùì•‚ä§ ‚Üí
           --------------------
           ùëé == ùëè ‚àà ùìîùìµ‚Çñ‚Çä‚ÇÅ (·∂ú V‚ä§)

   ùìîùìµ‚Çñ‚Çä‚ÇÅ-‚ä• : ‚àÄ{ùëé ùëè} ‚Üí
           ùëé == ùëè ‚àà ùì•‚ä• ‚Üí
           -------------------
           ùëé == ùëè ‚àà ùìîùìµ‚Çñ‚Çä‚ÇÅ (·∂ú V‚ä•)

   ùìîùìµ‚Çñ‚Çä‚ÇÅ-Ty-Ne : ‚àÄ {NE ne ne'} ‚Üí
           ne == ne' ‚àà ùì•Ty-Ne (suc ùìÄ) NE ‚Üí
           -------------------------------------------
           ne == ne' ‚àà ùìîùìµ‚Çñ‚Çä‚ÇÅ (‚Üë‚ü® (·∂ú (VùëÜùëíùë° (suc ùìÄ))) ‚ü© NE)

   ùìîùìµ‚Çñ‚Çä‚ÇÅ-ùëÜùëíùë°-pred : ‚àÄ{ùê¥ ùê¥'} ‚Üí
           ùê¥ == ùê¥' ‚àà ùì¢‚Çñ ‚Üí
           -------------------------
           ùê¥ == ùê¥' ‚àà ùìîùìµ‚Çñ‚Çä‚ÇÅ (·∂ú VùëÜùëíùë° ùìÄ)

   ùìîùìµ‚Çñ‚Çä‚ÇÅ-ùëÜùëíùë°-below : ‚àÄ{ùê¥ ùê¥' ùìÅ} ‚Üí
           ùìÅ <‚Ä≤ ùìÄ ‚Üí
           ùê¥ == ùê¥' ‚àà ùìîùìµ‚Çñ (·∂ú VùëÜùëíùë° ùìÅ) ‚Üí
           --------------------------
           ùê¥ == ùê¥' ‚àà ùìîùìµ‚Çñ‚Çä‚ÇÅ (·∂ú VùëÜùëíùë° ùìÅ)

   ùìîùìµ‚Çñ‚Çä‚ÇÅ-‚ü™Type‚ãØ‚ü´ : ‚àÄ{ùëÜ ùëá ùê¥ ùê¥'} ‚Üí
           ùê¥ == ùê¥' ‚àà ùì•Type (ùìîùìµ‚Çñ ùëÜ) (ùìîùìµ‚Çñ ùëá) ‚Üí
           ----------------------------------
           ùê¥ == ùê¥' ‚àà ùìîùìµ‚Çñ‚Çä‚ÇÅ ‚ü™Type ùëÜ ‚ãØ ùëá ‚ü´

   ùìîùìµ‚Çñ‚Çä‚ÇÅ-Œ† : ‚àÄ{ùê¥ ùêπ ùêπ¬∑ùëé ùëì ùëì' ùëé ùëé' ùëè ùëè'} ‚Üí

           (‚àÄ{ ùëé ùëé' } ‚Üí
             ùëé == ùëé' ‚àà ùìîùìµ‚Çñ‚Çä‚ÇÅ {ùìÄ} {ùì¢‚Çñ} {ùìîùìµ‚Çñ} ùê¥ ‚Üí
             ùêπ  ¬∑ ùëé  ‚áì ùêπ¬∑ùëé ‚Üí
             ùëì  ¬∑ ùëé  ‚áì ùëè ‚Üí
             ùëì' ¬∑ ùëé' ‚áì ùëè' ‚Üí
             ùëè == ùëè' ‚àà ùìîùìµ‚Çñ‚Çä‚ÇÅ {ùìÄ} {ùì¢‚Çñ} {ùìîùìµ‚Çñ} ùêπ¬∑ùëé) ‚Üí
           ------------------------------------
           ùëì == ùëì' ‚àà ùìîùìµ‚Çñ‚Çä‚ÇÅ (VŒ† ùê¥ ùêπ)

ùì¢ùìÆùìΩ : ‚Ñí ‚Üí Rel
ùìîùìµ : ‚Ñí ‚Üí ùïç ‚Üí Rel

ùì¢ùìÆùìΩ 0 = ùì¢ùìÆùìΩ‚ÇÄ
ùì¢ùìÆùìΩ (suc ùìÄ) = ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ {ùìÄ} {ùì¢ùìÆùìΩ ùìÄ} {ùìîùìµ ùìÄ}

ùìîùìµ 0 = ùìîùìµ‚ÇÄ
ùìîùìµ (suc ùìÄ) = ùìîùìµ‚Çñ‚Çä‚ÇÅ {ùìÄ} {ùì¢ùìÆùìΩ ùìÄ} {ùìîùìµ ùìÄ}

-- -- TOOD It might also be beneficial if we have proper inversion lemmas for these
ùì¢ùìÆùìΩ-NE : ‚àÄ{ùìÄ NE NE'} ‚Üí
  NE == NE' ‚àà ùìëùì∏ùìΩ ‚Üí
  --------------------------------------------------
  ‚Üë‚ü® (·∂ú (VùëÜùëíùë° ùìÄ)) ‚ü© NE == ‚Üë‚ü® (·∂ú (VùëÜùëíùë° ùìÄ)) ‚ü© NE' ‚àà ùì¢ùìÆùìΩ ùìÄ

ùì¢ùìÆùìΩ-ùëÅ : ‚àÄ{ùìÄ} ‚Üí (·∂ú VùëÅ) == (·∂ú VùëÅ) ‚àà ùì¢ùìÆùìΩ ùìÄ

ùì¢ùìÆùìΩ-‚ä§ : ‚àÄ{ùìÄ} ‚Üí (·∂ú V‚ä§) == (·∂ú V‚ä§) ‚àà ùì¢ùìÆùìΩ ùìÄ

ùì¢ùìÆùìΩ-‚ä• : ‚àÄ{ùìÄ} ‚Üí (·∂ú V‚ä•) == (·∂ú V‚ä•) ‚àà ùì¢ùìÆùìΩ ùìÄ

ùì¢ùìÆùìΩ-‚ü™Type‚ãØ‚ü´ : ‚àÄ{ùìÄ ùëÜ ùëÜ' ùëá ùëá'} ‚Üí
  ùëÜ == ùëÜ' ‚àà ùì¢ùìÆùìΩ ùìÄ ‚Üí
  ùëá == ùëá' ‚àà ùì¢ùìÆùìΩ ùìÄ ‚Üí
  -----------------------------------------------
  ‚ü™Type ùëÜ ‚ãØ ùëá ‚ü´ == ‚ü™Type ùëÜ' ‚ãØ ùëá' ‚ü´ ‚àà ùì¢ùìÆùìΩ (suc ùìÄ)

open import Data.Nat.Properties using (<-irrefl; ‚â§‚Ä≤‚áí‚â§)
open _‚â§‚Ä≤_
ùì¢ùìÆùìΩ‚Çó‚ààùì¢ùìÆùìΩ‚Çñ : ‚àÄ{ùìÅ ùìÄ} ‚Üí ùìÅ <‚Ä≤ ùìÄ ‚Üí (·∂ú (VùëÜùëíùë° ùìÅ)) == (·∂ú (VùëÜùëíùë° ùìÅ)) ‚àà ùì¢ùìÆùìΩ ùìÄ

ùì¢ùìÆùìΩ-Œ† : ‚àÄ{ùìÄ ùê¥ ùê¥' ùêπ ùêπ'} ‚Üí
  ùê¥ == ùê¥' ‚àà ùì¢ùìÆùìΩ ùìÄ ‚Üí
  (‚àÄ{ùëé ùëé'} ‚Üí ùëé == ùëé' ‚àà (ùìîùìµ ùìÄ ùê¥) ‚Üí
             (‚àÉ[ ùêµ ] ‚àÉ[ ùêµ' ] ( (ùêπ ¬∑ ùëé ‚áì ùêµ) √ó (ùêπ' ¬∑ ùëé' ‚áì ùêµ') √ó (ùêµ == ùêµ' ‚àà ùì¢ùìÆùìΩ ùìÄ) ))) ‚Üí
  -------------------------------------------------------------------------------------
  VŒ† ùê¥ ùêπ == VŒ† ùê¥' ùêπ' ‚àà ùì¢ùìÆùìΩ ùìÄ

ùìîùìµ-ùëÅ : ‚àÄ {ùìÄ} ‚Üí ùìîùìµ ùìÄ (·∂ú VùëÅ) ‚â°·¥ø ùìùùì™ùìΩ

ùìîùìµ-‚ä§ : ‚àÄ {ùìÄ} ‚Üí ùìîùìµ ùìÄ (·∂ú V‚ä§) ‚â°·¥ø ùì•‚ä§

ùìîùìµ-‚ä• : ‚àÄ {ùìÄ} ‚Üí ùìîùìµ ùìÄ (·∂ú V‚ä•) ‚â°·¥ø ùì•‚ä•

ùìîùìµ-ùëÜùëíùë° : ‚àÄ {ùìÅ ùìÄ} ‚Üí ùìÅ <‚Ä≤ ùìÄ ‚Üí ùìîùìµ ùìÄ (·∂ú (VùëÜùëíùë° ùìÅ)) ‚â°·¥ø ùì¢ùìÆùìΩ ùìÅ
ùìîùìµ-ùëÜùëíùë° {ùìÅ} {.(suc ùìÅ)} ‚â§‚Ä≤-refl = (Œª{ (ùìîùìµ‚Çñ‚Çä‚ÇÅ-ùëÜùëíùë°-pred x) ‚Üí x ;
                                   (ùìîùìµ‚Çñ‚Çä‚ÇÅ-ùëÜùëíùë°-below x x‚ÇÅ) ‚Üí ‚ä•-elim (<-irrefl refl (‚â§‚Ä≤‚áí‚â§ x))}) ,
                                ùìîùìµ‚Çñ‚Çä‚ÇÅ-ùëÜùëíùë°-pred
ùìîùìµ-ùëÜùëíùë° {ùìÅ} {(suc ùìÄ')} (‚â§‚Ä≤-step ùìÅ<ùìÄ') with ùìîùìµ-ùëÜùëíùë° ùìÅ<ùìÄ'
... | fst , snd = left , right
  where
    left : ùìîùìµ‚Çñ‚Çä‚ÇÅ (·∂ú VùëÜùëíùë° ùìÅ) ‚äÜ ùì¢ùìÆùìΩ ùìÅ
    left (ùìîùìµ‚Çñ‚Çä‚ÇÅ-ùëÜùëíùë°-pred x) = x
    left (ùìîùìµ‚Çñ‚Çä‚ÇÅ-ùëÜùëíùë°-below x x‚ÇÅ) = fst x‚ÇÅ
    right : ùì¢ùìÆùìΩ ùìÅ ‚äÜ ùìîùìµ‚Çñ‚Çä‚ÇÅ (·∂ú VùëÜùëíùë° ùìÅ)
    right SetùìÅab = ùìîùìµ‚Çñ‚Çä‚ÇÅ-ùëÜùëíùë°-below ùìÅ<ùìÄ' (snd SetùìÅab)

ùìîùìµ-‚ü™Type‚ãØ‚ü´ : ‚àÄ {ùìÄ ùëÜ ùëá} ‚Üí ùìîùìµ (suc ùìÄ) ‚ü™Type ùëÜ ‚ãØ ùëá ‚ü´ ‚â°·¥ø ùì•Type (ùìîùìµ ùìÄ ùëÜ) (ùìîùìµ ùìÄ ùëá)

ùìîùìµ-NE : ‚àÄ {ùìÄ NE} ‚Üí ùìîùìµ ùìÄ (‚Üë‚ü® (·∂ú (VùëÜùëíùë° ùìÄ)) ‚ü© NE) ‚â°·¥ø ùì•Ty-Ne ùìÄ NE

-- proofs are mostly straightforward
ùìîùìµ-ùëÅ {zero} = (Œª{ (ùìîùìµ‚ÇÄ-ùëÅ x) ‚Üí x}) , (Œª{ x ‚Üí ùìîùìµ‚ÇÄ-ùëÅ x})
ùìîùìµ-ùëÅ {suc ùìÄ} = (Œª{ (ùìîùìµ‚Çñ‚Çä‚ÇÅ-ùëÅ x) ‚Üí x}) , (Œª{ x ‚Üí ùìîùìµ‚Çñ‚Çä‚ÇÅ-ùëÅ x})

ùìîùìµ-‚ä§ {zero} = (Œª{ (ùìîùìµ‚ÇÄ-‚ä§ x) ‚Üí x}) , (Œª{ x ‚Üí ùìîùìµ‚ÇÄ-‚ä§ x})
ùìîùìµ-‚ä§ {suc ùìÄ} = (Œª{ (ùìîùìµ‚Çñ‚Çä‚ÇÅ-‚ä§ x) ‚Üí x}) , (Œª{ x ‚Üí ùìîùìµ‚Çñ‚Çä‚ÇÅ-‚ä§ x})

ùìîùìµ-‚ä• {zero} = (Œª{ (ùìîùìµ‚ÇÄ-‚ä• x) ‚Üí x}) , (Œª{ x ‚Üí ùìîùìµ‚ÇÄ-‚ä• x})
ùìîùìµ-‚ä• {suc ùìÄ} = (Œª{ (ùìîùìµ‚Çñ‚Çä‚ÇÅ-‚ä• x) ‚Üí x}) , (Œª{ x ‚Üí ùìîùìµ‚Çñ‚Çä‚ÇÅ-‚ä• x})

ùìîùìµ-‚ü™Type‚ãØ‚ü´ = (Œª{ (ùìîùìµ‚Çñ‚Çä‚ÇÅ-‚ü™Type‚ãØ‚ü´ x) ‚Üí x}) ,
               Œª{ (ùì•Type-ne x x‚ÇÅ x‚ÇÇ) ‚Üí ùìîùìµ‚Çñ‚Çä‚ÇÅ-‚ü™Type‚ãØ‚ü´ (ùì•Type-ne x x‚ÇÅ x‚ÇÇ) ;
                  (ùì•Type-val x x‚ÇÅ) ‚Üí ùìîùìµ‚Çñ‚Çä‚ÇÅ-‚ü™Type‚ãØ‚ü´ (ùì•Type-val x x‚ÇÅ)}

ùìîùìµ-NE {zero} = (Œª{ (ùìîùìµ‚ÇÄ-Ty-Ne x) ‚Üí x}) , Œª{ (ùì•Ty-Ne-mem x x‚ÇÅ x‚ÇÇ) ‚Üí ùìîùìµ‚ÇÄ-Ty-Ne (ùì•Ty-Ne-mem x x‚ÇÅ x‚ÇÇ)}
ùìîùìµ-NE {suc ùìÄ} = (Œª{ (ùìîùìµ‚Çñ‚Çä‚ÇÅ-Ty-Ne x) ‚Üí x}) , (Œª{ (ùì•Ty-Ne-mem x x‚ÇÅ x‚ÇÇ) ‚Üí ùìîùìµ‚Çñ‚Çä‚ÇÅ-Ty-Ne (ùì•Ty-Ne-mem x x‚ÇÅ x‚ÇÇ)})

ùì¢ùìÆùìΩ-NE {zero} = ùì¢ùìÆùìΩ‚ÇÄ-NE
ùì¢ùìÆùìΩ-NE {suc ùìÄ} = ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-NE

ùì¢ùìÆùìΩ-ùëÅ {zero} = ùì¢ùìÆùìΩ‚ÇÄ-ùëÅ
ùì¢ùìÆùìΩ-ùëÅ {suc ùìÄ} = ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-ùëÅ

ùì¢ùìÆùìΩ-‚ä§ {zero} = ùì¢ùìÆùìΩ‚ÇÄ-‚ä§
ùì¢ùìÆùìΩ-‚ä§ {suc ùìÄ} = ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-‚ä§

ùì¢ùìÆùìΩ-‚ä• {zero} = ùì¢ùìÆùìΩ‚ÇÄ-‚ä•
ùì¢ùìÆùìΩ-‚ä• {suc ùìÄ} = ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-‚ä•

ùì¢ùìÆùìΩ-‚ü™Type‚ãØ‚ü´ ùëÜ==ùëÜ' ùëá==ùëá' = ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-‚ü™Type‚ãØ‚ü´ ùëÜ==ùëÜ' ùëá==ùëá'

ùì¢ùìÆùìΩ‚Çó‚ààùì¢ùìÆùìΩ‚Çñ {ùìÅ} {.(suc ùìÅ)} ‚â§‚Ä≤-refl = ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-ùì¢ùìÆùìΩ‚Çñ
ùì¢ùìÆùìΩ‚Çó‚ààùì¢ùìÆùìΩ‚Çñ {ùìÅ} {.(suc _)} (‚â§‚Ä≤-step ùìÅ<ùìÄ) = ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-‚äáùì¢‚Çñ (ùì¢ùìÆùìΩ‚Çó‚ààùì¢ùìÆùìΩ‚Çñ ùìÅ<ùìÄ)

ùì¢ùìÆùìΩ-Œ† {zero} = ùì¢ùìÆùìΩ‚ÇÄ-Œ†
ùì¢ùìÆùìΩ-Œ† {suc ùìÄ} = ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-Œ†

-- Another litmus test
open import Data.Nat.Properties using (‚â§‚Ä≤-trans)
SetùìÅ‚ààSetùìÄ‚ÜíùìÅ<ùìÄ : ‚àÄ{ùìÄ ùìÅ} ‚Üí (·∂ú (VùëÜùëíùë° ùìÅ)) == (·∂ú (VùëÜùëíùë° ùìÅ)) ‚àà ùì¢ùìÆùìΩ ùìÄ ‚Üí ùìÅ <‚Ä≤ ùìÄ
SetùìÅ‚ààSetùìÄ‚ÜíùìÅ<ùìÄ {suc ùìÄ} {ùìÅ} (ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-‚äáùì¢‚Çñ x) with SetùìÅ‚ààSetùìÄ‚ÜíùìÅ<ùìÄ x
... | y = ‚â§‚Ä≤-trans y (‚â§‚Ä≤-step ‚â§‚Ä≤-refl)
SetùìÅ‚ààSetùìÄ‚ÜíùìÅ<ùìÄ {suc ùìÄ} {.ùìÄ} ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-ùì¢ùìÆùìΩ‚Çñ = ‚â§‚Ä≤-refl

ùìÅ<ùìÄ‚ÜíSetùìÅ‚ààSetùìÄ : ‚àÄ{ùìÄ ùìÅ} ‚Üí ùìÅ <‚Ä≤ ùìÄ ‚Üí (·∂ú (VùëÜùëíùë° ùìÅ)) == (·∂ú (VùëÜùëíùë° ùìÅ)) ‚àà ùì¢ùìÆùìΩ ùìÄ
ùìÅ<ùìÄ‚ÜíSetùìÅ‚ààSetùìÄ {.(suc ùìÅ)} {ùìÅ} ‚â§‚Ä≤-refl = ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-ùì¢ùìÆùìΩ‚Çñ
ùìÅ<ùìÄ‚ÜíSetùìÅ‚ààSetùìÄ {.(suc _)} {ùìÅ} (‚â§‚Ä≤-step ùìÅ<ùìÄ) = ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-‚äáùì¢‚Çñ (ùìÅ<ùìÄ‚ÜíSetùìÅ‚ààSetùìÄ ùìÅ<ùìÄ)

predicativity : ‚àÄ{ùìÅ} ‚Üí (·∂ú (VùëÜùëíùë° ùìÅ)) == (·∂ú (VùëÜùëíùë° ùìÅ)) ‚àà ùì¢ùìÆùìΩ ùìÅ ‚Üí ‚ä•
predicativity SetùìÅ‚ààSetùìÅ = ‚ä•-elim (<-irrefl refl (‚â§‚Ä≤‚áí‚â§ (SetùìÅ‚ààSetùìÄ‚ÜíùìÅ<ùìÄ SetùìÅ‚ààSetùìÅ)))

cumulativity : ‚àÄ{ùìÅ ùìÄ} ‚Üí ùìÅ ‚â§‚Ä≤ ùìÄ ‚Üí ùì¢ùìÆùìΩ ùìÅ ‚äÜ ùì¢ùìÆùìΩ ùìÄ
cumulativity {ùìÅ} {.ùìÅ} ‚â§‚Ä≤-refl a==b‚ààùì¢ùìÆùìΩùìÅ = a==b‚ààùì¢ùìÆùìΩùìÅ
cumulativity {ùìÅ} {(suc ùìÄ')} (‚â§‚Ä≤-step ùìÅ<ùìÄ') a==b‚ààùì¢ùìÆùìΩùìÅ with cumulativity ùìÅ<ùìÄ'
... | SetùìÅ‚äÜSetùìÄ' = ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-‚äáùì¢‚Çñ (SetùìÅ‚äÜSetùìÄ' a==b‚ààùì¢ùìÆùìΩùìÅ)
-- we make our live easy with the extra constructor ùì¢ùìÆùìΩ‚Çñ‚Çä‚ÇÅ-‚äáùì¢‚Çñ, which is not present in Abel's thesis
-- it also seems mandatory to have

-- -- TODO show that all these things are PERs/closed under PER formation
-- TODO turn these into instances once proven
-- TODO prove that element function is a function?
Per-ùì¢ùìÆùìΩ‚ÇÄ : Per ùì¢ùìÆùìΩ‚ÇÄ
ùìîùìµ‚ÇÄ‚à∂ùì¢ùìÆùìΩ‚ÇÄ‚üπPer :  ùìîùìµ‚ÇÄ ‚à∂ ùì¢ùìÆùìΩ‚ÇÄ ‚üπPer

Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (.(‚Üë‚ü® ·∂ú VùëÜùëíùë° 0 ‚ü© _) , inj‚ÇÅ (ùì¢ùìÆùìΩ‚ÇÄ-NE x)) = ùì¢ùìÆùìΩ‚ÇÄ-NE (per-refl (memÀ° {_} {ùìëùì∏ùìΩ} x))
Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (.(·∂ú VùëÅ) , inj‚ÇÅ ùì¢ùìÆùìΩ‚ÇÄ-ùëÅ) = ùì¢ùìÆùìΩ‚ÇÄ-ùëÅ
Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (.(·∂ú V‚ä§) , inj‚ÇÅ ùì¢ùìÆùìΩ‚ÇÄ-‚ä§) = ùì¢ùìÆùìΩ‚ÇÄ-‚ä§
Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (.(·∂ú V‚ä•) , inj‚ÇÅ ùì¢ùìÆùìΩ‚ÇÄ-‚ä•) = ùì¢ùìÆùìΩ‚ÇÄ-‚ä•
Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (.(VŒ† _ _) , inj‚ÇÅ (ùì¢ùìÆùìΩ‚ÇÄ-Œ† {ùê¥} {ùê¥'} {ùêπ} {ùêπ'} x x‚ÇÅ)) = ùì¢ùìÆùìΩ‚ÇÄ-Œ† (Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (memÀ° {_} {ùì¢ùìÆùìΩ‚ÇÄ} x)) {!!}
  -- ùì¢ùìÆùìΩ‚ÇÄ-Œ† (Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (memÀ° {_} {ùì¢ùìÆùìΩ‚ÇÄ} x)) prf
  --   where
  --     prf : ‚àÄ {ùëé ùëé' : ùïç} ‚Üí ùëé == ùëé' ‚àà ùìîùìµ‚ÇÄ ùê¥ ‚Üí
  --           ‚àÉ[ ùêµ ] ‚àÉ[ ùêµ' ] ( (ùêπ ¬∑ ùëé ‚áì ùêµ) √ó (ùêπ ¬∑ ùëé' ‚áì ùêµ') √ó (ùêµ == ùêµ' ‚àà ùì¢ùìÆùìΩ‚ÇÄ))
  --     prf ElAaa' with x‚ÇÅ ElAaa' | x‚ÇÅ (Per.per-sym (per-family ùìîùìµ‚ÇÄ‚à∂ùì¢ùìÆùìΩ‚ÇÄ‚üπPer (memÀ° {_} {ùì¢ùìÆùìΩ‚ÇÄ} x)) ElAaa') | x‚ÇÅ (Per.per-refl (per-family ùìîùìµ‚ÇÄ‚à∂ùì¢ùìÆùìΩ‚ÇÄ‚üπPer (memÀ° {_} {ùì¢ùìÆùìΩ‚ÇÄ} x)) (memÀ° {_} {ùìîùìµ‚ÇÄ ùê¥} ElAaa'))
  --     ... | B‚ÇÅ , B‚ÇÇ , F¬∑a‚áìB‚ÇÅ , F'¬∑a'‚áìB‚ÇÇ , B‚ÇÅ==B‚ÇÇ | B‚ÇÉ , B‚ÇÑ , F¬∑a'‚áìB‚ÇÉ , F'¬∑a‚áìB‚ÇÑ , B‚ÇÉ==B‚ÇÑ | B‚ÇÅ' , B‚ÇÑ' , F¬∑a‚áìB‚ÇÅ' , F'¬∑a‚áìB‚ÇÑ' , B‚ÇÅ'==B‚ÇÑ' rewrite is-fun-¬∑ F¬∑a‚áìB‚ÇÅ' F¬∑a‚áìB‚ÇÅ | is-fun-¬∑ F'¬∑a‚áìB‚ÇÑ' F'¬∑a‚áìB‚ÇÑ =
  --      B‚ÇÅ , (B‚ÇÉ , F¬∑a‚áìB‚ÇÅ , ( F¬∑a'‚áìB‚ÇÉ , Per.per-trans Per-ùì¢ùìÆùìΩ‚ÇÄ B‚ÇÅ'==B‚ÇÑ' (Per.per-sym Per-ùì¢ùìÆùìΩ‚ÇÄ B‚ÇÉ==B‚ÇÑ)))

Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (.(‚Üë‚ü® ·∂ú VùëÜùëíùë° 0 ‚ü© _) , inj‚ÇÇ (ùì¢ùìÆùìΩ‚ÇÄ-NE x)) = ùì¢ùìÆùìΩ‚ÇÄ-NE (per-refl (mem ≥ {_} {ùìëùì∏ùìΩ} x))
Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (.(·∂ú VùëÅ) , inj‚ÇÇ ùì¢ùìÆùìΩ‚ÇÄ-ùëÅ) = ùì¢ùìÆùìΩ‚ÇÄ-ùëÅ
Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (.(·∂ú V‚ä§) , inj‚ÇÇ ùì¢ùìÆùìΩ‚ÇÄ-‚ä§) = ùì¢ùìÆùìΩ‚ÇÄ-‚ä§
Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (.(·∂ú V‚ä•) , inj‚ÇÇ ùì¢ùìÆùìΩ‚ÇÄ-‚ä•) = ùì¢ùìÆùìΩ‚ÇÄ-‚ä•
Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (.(VŒ† _ _) , inj‚ÇÇ (ùì¢ùìÆùìΩ‚ÇÄ-Œ† {ùê¥} {ùê¥'} {ùêπ} {ùêπ'} x x‚ÇÅ)) =
  ùì¢ùìÆùìΩ‚ÇÄ-Œ† ((Per.per-refl Per-ùì¢ùìÆùìΩ‚ÇÄ (mem ≥ {_} {ùì¢ùìÆùìΩ‚ÇÄ} x))) (prf ‚àò massage)
    where
      massage : ùìîùìµ‚ÇÄ ùê¥' ‚äÜ ùìîùìµ‚ÇÄ ùê¥
      massage = Data.Product.proj‚ÇÇ (respect ùìîùìµ‚ÇÄ‚à∂ùì¢ùìÆùìΩ‚ÇÄ‚üπPer x)

      prf : ‚àÄ {ùëé ùëé' : ùïç} ‚Üí ùëé == ùëé' ‚àà ùìîùìµ‚ÇÄ ùê¥ ‚Üí
             ‚àÉ[ ùêµ ] ‚àÉ[ ùêµ' ] ( (ùêπ' ¬∑ ùëé ‚áì ùêµ) √ó (ùêπ' ¬∑ ùëé' ‚áì ùêµ') √ó (ùêµ == ùêµ' ‚àà ùì¢ùìÆùìΩ‚ÇÄ))
      prf ElAaa' with x‚ÇÅ ElAaa' | x‚ÇÅ (Per.per-sym (per-family ùìîùìµ‚ÇÄ‚à∂ùì¢ùìÆùìΩ‚ÇÄ‚üπPer (memÀ° {_} {ùì¢ùìÆùìΩ‚ÇÄ} x)) ElAaa') | x‚ÇÅ (Per.per-refl (per-family ùìîùìµ‚ÇÄ‚à∂ùì¢ùìÆùìΩ‚ÇÄ‚üπPer (memÀ° {_} {ùì¢ùìÆùìΩ‚ÇÄ} x)) (memÀ° {_} {ùìîùìµ‚ÇÄ ùê¥} ElAaa'))
      ... | B‚ÇÅ , B‚ÇÇ , F¬∑a‚áìB‚ÇÅ , F'¬∑a'‚áìB‚ÇÇ , B‚ÇÅ==B‚ÇÇ | B‚ÇÉ , B‚ÇÑ , F¬∑a'‚áìB‚ÇÉ , F'¬∑a‚áìB‚ÇÑ , B‚ÇÉ==B‚ÇÑ | B‚ÇÅ' , B‚ÇÑ' , F¬∑a‚áìB‚ÇÅ' , F'¬∑a‚áìB‚ÇÑ' , B‚ÇÅ'==B‚ÇÑ' rewrite is-fun-¬∑ F¬∑a‚áìB‚ÇÅ' F¬∑a‚áìB‚ÇÅ | is-fun-¬∑ F'¬∑a‚áìB‚ÇÑ' F'¬∑a‚áìB‚ÇÑ =
       B‚ÇÑ , (B‚ÇÇ , (F'¬∑a‚áìB‚ÇÑ , (F'¬∑a'‚áìB‚ÇÇ , Per.per-trans Per-ùì¢ùìÆùìΩ‚ÇÄ (Per.per-sym Per-ùì¢ùìÆùìΩ‚ÇÄ B‚ÇÅ'==B‚ÇÑ') B‚ÇÅ==B‚ÇÇ)))

Per.per-sym Per-ùì¢ùìÆùìΩ‚ÇÄ (ùì¢ùìÆùìΩ‚ÇÄ-NE x) = ùì¢ùìÆùìΩ‚ÇÄ-NE (per-sym x)
Per.per-sym Per-ùì¢ùìÆùìΩ‚ÇÄ ùì¢ùìÆùìΩ‚ÇÄ-ùëÅ = ùì¢ùìÆùìΩ‚ÇÄ-ùëÅ
Per.per-sym Per-ùì¢ùìÆùìΩ‚ÇÄ ùì¢ùìÆùìΩ‚ÇÄ-‚ä§ = ùì¢ùìÆùìΩ‚ÇÄ-‚ä§
Per.per-sym Per-ùì¢ùìÆùìΩ‚ÇÄ ùì¢ùìÆùìΩ‚ÇÄ-‚ä• = ùì¢ùìÆùìΩ‚ÇÄ-‚ä•
Per.per-sym Per-ùì¢ùìÆùìΩ‚ÇÄ (ùì¢ùìÆùìΩ‚ÇÄ-Œ† {ùê¥} {ùê¥'} {ùêπ} {ùêπ'} A==A' F==F') = ùì¢ùìÆùìΩ‚ÇÄ-Œ† (Per.per-sym Per-ùì¢ùìÆùìΩ‚ÇÄ A==A') (prf ‚àò massage)
  where
    massage : ùìîùìµ‚ÇÄ ùê¥' ‚äÜ ùìîùìµ‚ÇÄ ùê¥
    massage = Data.Product.proj‚ÇÇ (respect ùìîùìµ‚ÇÄ‚à∂ùì¢ùìÆùìΩ‚ÇÄ‚üπPer A==A')

    prf : ‚àÄ {ùëé} {ùëé'} ‚Üí
      ùëé' == ùëé ‚àà ùìîùìµ‚ÇÄ ùê¥ ‚Üí
      ‚àÉ-syntax
      (Œª ùêµ' ‚Üí
         ‚àÉ-syntax (Œª ùêµ ‚Üí (ùêπ' ¬∑ ùëé' ‚áì ùêµ') √ó (ùêπ ¬∑ ùëé ‚áì ùêµ) √ó (ùêµ' == ùêµ ‚àà ùì¢ùìÆùìΩ‚ÇÄ)))
    prf ElAa'a with F==F' (Per.per-sym (per-family ùìîùìµ‚ÇÄ‚à∂ùì¢ùìÆùìΩ‚ÇÄ‚üπPer (memÀ° {_} {ùì¢ùìÆùìΩ‚ÇÄ} A==A')) ElAa'a)
    ... | B , B' , ùêπ¬∑ùëé‚áìB , ùêπ'¬∑ùëé'‚áìB' , B==B' = B' , (B , (ùêπ'¬∑ùëé'‚áìB' , (ùêπ¬∑ùëé‚áìB , (Per.per-sym Per-ùì¢ùìÆùìΩ‚ÇÄ B==B')))) -- this'll cause termination check to complain :(
Per.per-trans Per-ùì¢ùìÆùìΩ‚ÇÄ = {!!}


respect ùìîùìµ‚ÇÄ‚à∂ùì¢ùìÆùìΩ‚ÇÄ‚üπPer = {!!}
Per.per-refl  (per-family ùìîùìµ‚ÇÄ‚à∂ùì¢ùìÆùìΩ‚ÇÄ‚üπPer {ùê¥} ùê¥‚ààùì¢ùìÆùìΩ‚ÇÄ) = {!!}
Per.per-sym   (per-family ùìîùìµ‚ÇÄ‚à∂ùì¢ùìÆùìΩ‚ÇÄ‚üπPer {ùê¥} ùê¥‚ààùì¢ùìÆùìΩ‚ÇÄ) = {!!}
Per.per-trans (per-family ùìîùìµ‚ÇÄ‚à∂ùì¢ùìÆùìΩ‚ÇÄ‚üπPer {ùê¥} ùê¥‚ààùì¢ùìÆùìΩ‚ÇÄ) = {!!}



-- Set of all semantic types is the limit ùì¢ùìÆùìΩœâ
ùì¢ùìÆùìΩœâ : Rel
ùì¢ùìÆùìΩœâ A B = ‚àÉ[ ùìÅ ]( A == B ‚àà (ùì¢ùìÆùìΩ ùìÅ) )
ùíØùìéùìÖ‚ÑØ = ùì¢ùìÆùìΩœâ

-- Value interpretation is the limit ùìîùìµœâ
ùìîùìµœâ : ùïç ‚Üí Rel
ùìîùìµœâ ùëá ùëé ùëè = ‚àÉ[ ùìÅ ]( ùëé == ùëè ‚àà ùìîùìµ ùìÅ ùëá )

-- Interpretation of syntactic types into semantic types (PERs)
open import dsube.NbE using (‚ü¶_‚üß_‚áì_)
open import dsube.Syntax using (Exp)

data [_]_‚áì_ : Exp ‚Üí Env ‚Üí Rel ‚Üí Set where
  ty-rel : ‚àÄ{T œÅ ùëá} ‚Üí
     ‚ü¶ T ‚üß œÅ ‚áì ùëá ‚Üí
     -----------------
     [ T ] œÅ ‚áì ùìîùìµœâ ùëá

-- -- TODO show that all these things live in the candidate space

-- -- TODO: semantically well-formed contexts and context extension (p. 46)

-- -- TODO: for each of the syntactic judgments, define the semantic jugdements (p. 46)
-- -- TODO: esp: how to deal with subtyping?

-- -- TODO : typed candidate spaces (p. 47)

-- -- TODO : completeness proof (end of 4.5, p. 48)


-- ```
